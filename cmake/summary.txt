#=======================================================================================================================
#
#   Copyright 2011, 2012, 2013, 2014, 2015, 2016, 2017 Institut fuer Neuroinformatik, Ruhr-Universitaet Bochum, Germany
# 
#   This file is part of cedar.
#
#   cedar is free software: you can redistribute it and/or modify it under
#   the terms of the GNU Lesser General Public License as published by the
#   Free Software Foundation, either version 3 of the License, or (at your
#   option) any later version.
#
#   cedar is distributed in the hope that it will be useful, but WITHOUT ANY
#   WARRANTY; without even the implied warranty of MERCHANTABILITY or
#   FITNESS FOR A PARTICULAR PURPOSE. See the GNU Lesser General Public
#   License for more details.
#
#   You should have received a copy of the GNU Lesser General Public License
#   along with cedar. If not, see <http://www.gnu.org/licenses/>.
#
#=======================================================================================================================
#
#   Institute:   Ruhr-Universitaet Bochum
#                Institut fuer Neuroinformatik
#
#   File:        summary.txt
#
#   Maintainer:  Oliver Lomp
#   Email:       oliver.lomp@ini.rub.de
#   Date:        2013 01 08
#
#   Description: Outputs a summary of the configuration of cedar.
#
#   Credits:
#
#=======================================================================================================================

set(CEDAR_SUMMARY_LINE_LENGTH 60)

# some useful macros ===================================================================================================

macro(fill_line CHAR)
  string(RANDOM LENGTH ${CEDAR_SUMMARY_LINE_LENGTH} ALPHABET "${CHAR}" line)
  message("${line}")
endmacro(fill_line)

macro(cedar_summary_heading CAPTION CHAR)
  message("")
  fill_line(${CHAR})
  message(" ${CAPTION}")
  fill_line(${CHAR})
  message("")
endmacro(cedar_summary_heading)

macro(cedar_summary_h1 CAPTION)
  cedar_summary_heading(${CAPTION} "=")
endmacro(cedar_summary_h1)

macro(cedar_summary_h2 CAPTION)
  cedar_summary_heading(${CAPTION} "-")
endmacro(cedar_summary_h2)

macro(cedar_summary_h3 CAPTION)
  string(LENGTH ${CAPTION} cap_length)
  math(EXPR num_filler "${CEDAR_SUMMARY_LINE_LENGTH} - 4 - (${cap_length})")
  string(RANDOM LENGTH ${num_filler} ALPHABET "-" separator)
  message("-- ${CAPTION} ${separator}")  
  message("")
endmacro(cedar_summary_h3)

macro(cedar_summary_table_entry LEFT RIGHT)
  string(LENGTH ${LEFT} left_length)
  string(LENGTH ${RIGHT} right_length)
  
  # create a "random" string consisting of the appropriate amount of dots
  math(EXPR num_dots "${CEDAR_SUMMARY_LINE_LENGTH} - 6 - (${left_length} + ${right_length})")
  string(RANDOM LENGTH ${num_dots} ALPHABET "." dots)
  message("  ${LEFT} ${dots} ${RIGHT}")
endmacro(cedar_summary_table_entry)

macro(cedar_summary_lib_found LIB_NAME LIB_FOUND)
  if (${LIB_FOUND})
    set(lib_found_str "Yes")
    if (${ARGV2})
      set(lib_name_str "${LIB_NAME} ${ARGV2}")
    else()
      set(lib_name_str "${LIB_NAME}")
    endif()
  else()
    set(lib_found_str "No")
    set(lib_name_str "${LIB_NAME}")
  endif()
  cedar_summary_table_entry(${lib_name_str} ${lib_found_str})    
endmacro(cedar_summary_lib_found)

macro(add_name_if_found PLOT_LIBS LIB_NAME LIB_FOUND)
  if(${LIB_FOUND})
    set(PLOT_LIBS "${${PLOT_LIBS}} ${LIB_NAME}")
  endif()
endmacro(add_name_if_found)

macro(set_available_plot_lib_names)
  foreach(arg ${ARGN})
    add_name_if_found(PLOT_LIBS ${arg} "${arg}_FOUND")
  endforeach()
  if(PLOT_LIBS STREQUAL "")
    set(PLOT_LIBS "none")
  endif()
endmacro(set_available_plot_lib_names)


# end of macros ========================================================================================================


cedar_summary_h1("Configuration")


cedar_summary_h2("Mandatory Libraries")

cedar_summary_lib_found("Qt" QT_FOUND ${QT_VERSION_MAJOR})
cedar_summary_lib_found("Boost" Boost_FOUND)
cedar_summary_lib_found("OpenCV" OpenCV_FOUND)

message("")

cedar_summary_h2("Optional Libraries")

cedar_summary_h3("Plot Libraries")

cedar_summary_lib_found("Qt5.7" QT5_FOUND)
cedar_summary_lib_found("Qwt" qwt_FOUND)
cedar_summary_lib_found("QwtPlot3d" qwtplot3d_FOUND)
cedar_summary_lib_found("Vtk" CEDAR_USE_VTK ${VTK_MAJOR_VERSION})
message("")

set(PLOT_LIBS "")
set_available_plot_lib_names("qwt" "VTK")
cedar_summary_table_entry("Libraries for Line-Plots" ${PLOT_LIBS})
set(PLOT_LIBS "")
set_available_plot_lib_names("cedar")
cedar_summary_table_entry("Libraries for Image-Plots" "cedar")
set(PLOT_LIBS "")
set_available_plot_lib_names("qwtplot3d" "VTK")
cedar_summary_table_entry("Libraries for Surface-Plots" ${PLOT_LIBS})
message("")

cedar_summary_h3("Misc. Libraries")

cedar_summary_lib_found("GLEW" GLEW_FOUND)
cedar_summary_lib_found("QGLViewer" QGLViewer_FOUND)
if(CEDAR_USE_FFTW_THREADED)
cedar_summary_lib_found("FFTW (multi-threaded)" CEDAR_USE_FFTW)
else(CEDAR_USE_FFTW_THREADED)
cedar_summary_lib_found("FFTW" CEDAR_USE_FFTW)
endif(CEDAR_USE_FFTW_THREADED)
cedar_summary_lib_found("Yarp" CEDAR_USE_YARP)
cedar_summary_lib_found("PCL" CEDAR_USE_PCL)
cedar_summary_lib_found("Eigen3" CEDAR_USE_PCL)

message("")

cedar_summary_h3("Hardware APIs")

cedar_summary_lib_found("LibDC1394" CEDAR_USE_LIB_DC1394)
cedar_summary_lib_found("FRI" CEDAR_USE_KUKA_LWR)
cedar_summary_lib_found("Amtec" CEDAR_USE_AMTEC)


message("")

cedar_summary_h2("Documentation")

cedar_summary_lib_found("Doxygen" DOXYGEN_FOUND)

message("")

cedar_summary_h2("Installation prefix")

message("  ${CMAKE_INSTALL_PREFIX}")

cedar_summary_h2("Issues (check these if you have problems compiling cedar)")

if ("${CEDAR_ISSUES}" STREQUAL "")
  message("  none")
else ("${CEDAR_ISSUES}" STREQUAL "")
  foreach(issue ${CEDAR_ISSUES})
    message("  ${issue}")
  endforeach()
endif ("${CEDAR_ISSUES}" STREQUAL "")

message("")
fill_line("=")
message("")
